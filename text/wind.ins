# Windows_3.1_IOServer 3.0 300
set exitcode=0

if ($argv[1] != 0) then
#installation script
#argv[1] = 0 for uninstalling software, otherwise the software is installed
#argv[2] = dirname of the installation disk or tape drive
#argv[3] = installation medium "tape" or "disk"
  switch ($argv[3])
    case disk:
      echo Loading software from disk ...
  
      echo
      echo "[7mInstalling Helios MS Windows 3.1 I/O server and Support server[0m"

      echo Installing winsrvr.exe to /helios
      cp $argv[2]/winsrvr.exe /helios
      if ($status) then
	set exitcode=1
      endif

      test -d /helios/local/bin
      if ($status) then
	echo Creating directory /helios/local/bin
	set dir = /helios
	foreach sub_dir (local bin)
	  set dir = $dir/$sub_dir
	  mkdir $dir >& nul
	end
      endif
      echo Installing /helios/local/bin
      cp -r $argv[2]/local/bin/* /helios/local/bin 
      if ($status) then
	set exitcode=1
      endif

      clear
      echo
      echo "[7mPlease Note :[0m"
      echo "- The Helios MS Windows 3.1 I/O Server (winsrvr.exe) has been installed in"
      echo "  /helios directory. The MS Windows 3.1 Program Manager File-New menu option"
      echo "  should be used to install the application as a windows icon."
      echo "- A utility for graphically monitoring processor activity (mslights) has been"
      echo "  installed in /helios/local/bin."
      echo
      echo Press Enter to continue:
      set tempflag=$<
      clear
      echo
      echo "[7mLogin script[0m"
      echo "- The full functionality of the Windows I/O server requires the presence of"
      echo "  the Windows Support server (startws). This server can be started from "
      echo "  your login script as follows :"
      cat $argv[2]/users/guest/login
      echo "Do you wish to add these lines to your login script (y/n) ?"
      set tempflag=$<
      if ($tempflag == y || $tempflag == Y) then
	fgrep -s "startws" ~/login
	if ($status) then
	  echo Amending login script ...
	  cat $argv[2]/users/guest/login >> ~/login
	else
	  echo Warning startws found in login file, no changes made to login
	endif
      else
	echo Leaving login file unchanged
      endif 

      clear
      echo
      echo Installation complete
      breaksw

    case tape:
      echo No tape support for loadpac yet
      breaksw
    endsw
  else
#remove instructions
    echo Deleting Helios MS Windows 3.1 I/O Server

    rm -f /helios/winsrvr.exe 
    rm -f /helios/local/bin/startws /helios/local/bin/mslights

    echo Helios MS Windows 3.1 I/O Server Deleted
  endif

exit $exitcode
