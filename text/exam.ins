# Examples 1.22 210
set exitcode=0

set target=/helios/users/guest/examples
set bak=$target.121

if ( $argv[1] != 0 ) then

# installation script
  #argv[1] = 0 for uninstalling software, otherwise the software is installed
  #argv[2] = dirname of the installation disk or tape drive
  #argv[3] = installation medium "tape" or "disk"

  switch ( $argv[3] )
    case disk:
      echo Loading software from disk ...

      test -d $target
      if ( $status == 0 ) then
        echo Examples directory: $target already exists
        echo Type "'y'" to remove directory
        echo Type any other key to rename it: $bak
        set tempflag=$<
        if ( "$tempflag" == "y" || "$tempflag" == "Y" ) then
          echo Removing directory ...
          rm -r $target
        else
          echo Moving $target to $bak ...
          mv $target $bak
        endif
        unset tempflag
      endif

      echo Copying examples directories to: $target ...

      cp -r $argv[2]/examples $target
      if( $status != 0 ) then
        set exitcode=1
      endif

      rehash
      if ( $exitcode == 0 ) then
        echo Installation complete
      endif

      breaksw
    case tape:
       echo No tape support for loadpac yet
       breaksw
    endsw
else
# remove instructions
    test -d $target
    if ( $status == 0 ) then
        echo Deleting directory: $target
        rm -r $target
    endif

    rehash
    echo Package removed
endif
unset bak
unset target     
exit $exitcode
