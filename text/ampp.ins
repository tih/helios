# Ampp 1.2 128
set exitcode=0
if ($argv[1] != 0) then
echo Installing Helios Assembler Macro Pre-processor
#installation script
  #argv[1] = 0 for uninstalling software, otherwise the software is installed
  #argv[2] = dirname of the installation disk or tape drive
  #argv[3] = installation medium "tape" or "disk"
  switch ($argv[3])
    case disk:
      echo Loading software from disk ...

      test -d /helios/include
      if ( $status == 1 ) then
        echo Making directory /helios/include
        mkdir /helios/include
      endif

      test -d /helios/include/ampp
      if ( $status == 1 ) then
        echo Making directory /helios/include/ampp
        mkdir /helios/include/ampp
      endif

      echo Copying include files into /helios/include/ampp ...

      cp -r $argv[2]/include/ampp/* /helios/include/ampp
      if($status != 0) then
        set exitcode=1
      endif

# assumes bin directory already present...

      echo Copying ampp into the /helios/bin directory ...
      cp $argv[2]/bin/ampp /helios/bin
      if($status != 0) then
        set exitcode=1
      endif
      rehash
      echo Installation complete
      breaksw
    case tape:
       echo No tape support for loadpac yet
       breaksw
    endsw
else
#remove instructions
    echo Deleting Helios Assembler Macro Preprocessor

    test -d /helios/include/ampp
    if ($status == 0) then
	echo Type ""Y"" to remove /helios/include/ampp or any other key
	echo to preserve the directory.
	set tempflag=$<
	if ( $tempflag == y || $tempflag == Y ) then
	      echo Removing /helios/include/ampp directory ...
	      rm -r /helios/include/ampp
	else
	      echo Preserving /helios/include/ampp directory
	endif
	unset tempflag
      
    endif

    test -f /helios/bin/ampp
    if ($status == 0) then
        echo Removing ampp
        rm /helios/bin/ampp
    endif


    test -f /helios/system/ampp.ins
    if ($status == 0) then
        rm /helios/system/ampp.ins
    endif
    rehash
    echo Package removed
endif
exit $exitcode
