# Source_Debugger 2.0 127
# $Id: sour.ins,v 1.4 1992/05/20 10:16:45 craig Exp $
set exitcode=0

set version=V2.0
set lib_files = "/helios/lib/tla /helios/lib/dlib /helios/lib/dlib.def"
set etc_files = "/helios/etc/debugrc /helios/etc/debug.hlp"
set exam_dir  = "/helios/users/guest/examples/tiddles"

if ($argv[1] != 0) then
echo Installing Helios Source Debugger $version
# installation script
  #argv[1] = 0 for uninstalling software, otherwise the software is installed
  #argv[2] = dirname of the installation disk or tape drive
  #argv[3] = installation medium "tape" or "disk"
  switch ($argv[3])
    case disk:

# Copy debug server and library files into /helios/lib ...
# assumes lib directory already present

      foreach target_file ($lib_files)
        echo "    Installing: $target_file"
        cp $argv[2]$target_file $target_file
      end

# Copy debug startup and help files into /helios/etc ...
# assumes etc directory already present

      foreach target_file ($etc_files)
        echo "    Installing: $target_file"
        cp $argv[2]$target_file $target_file
      end

# example (tiddles tutorial ...)

      echo "    Install tutorial example program (y/n) ?"
      set tempflag=$<
      if ($tempflag == y || $tempflag == Y) then
        test -d $exam_dir
        if ($status == 0) then
          echo "    Directory $exam_dir already exists"
          echo "    Tutorial example program not installed"
        else
          set sub_dirs = "users guest examples tiddles"
          set tut_dir = /helios
          foreach target_dir ($sub_dirs)
            set tut_dir = $tut_dir/$target_dir
            test -d $tut_dir
            if ($status == 1) then
              mkdir $tut_dir
            endif
          end
          echo "    Installing directory: $tut_dir"
          cp $argv[2]$tut_dir/* $tut_dir
        endif
      else
        echo "    Not installed"
      endif
      echo

      rehash
      echo Installation complete
      breaksw
    case tape:
       echo No tape support for loadpac yet
       breaksw
    endsw

else

# remove instructions
    echo Deleting Helios Source Debugger $version

    foreach target_file ($lib_files $etc_files)
      echo "    Removing file     : $target_file"
      rm -f $target_file
    end
    echo "    Removing directory: $exam_dir"
    rm -rf $exam_dir
    echo

    rehash
    echo Package removed
endif
exit $exitcode
