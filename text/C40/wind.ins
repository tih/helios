# Windows_3.1_IOServer 2.0 360
# $Id: wind.ins,v 1.2 1992/12/17 10:18:12 craig Exp $
set exitcode=0

# NOTE: if lib directory exists on installation disk, assume full installation
# (lib, bin, include, examples)
# Otherwise, only install I/O and Support servers

set full_install = 1
test -d $argv[2]/lib
if ($status) then
  set full_install = 0
endif

set mswin_examples = /helios/users/guest/examples/mswin
set mswin_include = /helios/include/mswin

if ($argv[1] != 0) then
#installation script
#argv[1] = 0 for uninstalling software, otherwise the software is installed
#argv[2] = dirname of the installation disk or tape drive
#argv[3] = installation medium "tape" or "disk"
  switch ($argv[3])
    case disk:
      echo Loading software from disk ...
  
      echo
      echo "[7mInstalling Helios MS Windows 3.1 I/O server and Support server[0m"

      echo Installing winsrvr.exe to /helios
      cp $argv[2]/winsrvr.exe /helios
      if ($status) then
        set exitcode=1
      endif

      test -d /helios/local/bin
      if ($status) then
        echo Creating directory /helios/local/bin
        set dir = /helios
        foreach sub_dir (local bin)
          set dir = $dir/$sub_dir
          mkdir $dir >& nul
        end
      endif
      echo Installing /helios/local/bin
      cp -r $argv[2]/local/bin/* /helios/local/bin 
      if ($status) then
        set exitcode=1
      endif

      if ($full_install) then

# Full installation (lib, bin, include, examples, ...)

        echo
        echo "[7mInstalling Helios MS Windows 3.1 Graphics library and Examples[0m"

	foreach dir (lib bin)
          echo Installing /helios/$dir
          cp -r $argv[2]/$dir/* /helios/$dir 
          if ($status) then
            set exitcode=1
          endif
        end

        test -d /helios/include
        if ($status) then
          echo Creating directory /helios/include
          mkdir /helios/include
        endif

        echo Installing /helios/include
        test -f /helios/include/graph.h
        if ($status) then
          cp $argv[2]/include/graph.h /helios/include 
          if ($status) then
            set exitcode=1
          endif
        else
          fgrep -si "MS-WINDOWS" /helios/include/graph.h
          if ($status) then
            echo "Warning: Non MS Windows graph.h found in /helios/include"
            echo "         Installing graph.h to $mswin_include directory"
            echo "         (#include directives will require modification for graph.h)"
            test -d $mswin_include 
            if ($status) then
              mkdir $mswin_include
            endif
            cp -r $argv[2]/include/graph.h $mswin_include
          else 
            cp -r $argv[2]/include/graph.h /helios/include
          endif
          if ($status) then
            set exitcode=1
          endif
        endif

        test -d /helios/users/guest/examples
        if ($status) then
          set dir = /helios
          foreach sub_dir (users guest examples)
            set dir = $dir/$sub_dir
            mkdir $dir >& nul
          end
        endif
        test -d $mswin_examples
        if ($status) then
          echo Creating directory $mswin_examples
          mkdir $mswin_examples
        endif
        echo Installing $mswin_examples
        cp -r $argv[2]/users/guest/examples/mswin/* $mswin_examples

      else

# NOT full installation

        echo
        echo "[7mHelios MS Windows 3.1 Graphics library and Examples not installed[0m"
        echo "(this software is available separately from Distributed Software Ltd)"

      endif

      echo
      echo "[7mPlease Note :[0m"
      echo "- The Helios MS Windows 3.1 I/O Server (winsrvr.exe) has been installed in your"
      echo "  /helios directory. The MS Windows 3.1 Program Manager File-New menu option"
      echo "  should be used to install the application as a windows icon."
      echo "- A utility for graphically monitoring processor activity (mslights) has been"
      echo "  installed in /helios/local/bin."
      echo

      echo
      echo Installation complete
      breaksw

    case tape:
      echo No tape support for loadpac yet
      breaksw
    endsw
  else
#remove instructions
    echo Deleting Helios MS Windows 3.1 I/O Server

    rm -f /helios/bin/printgrf /helios/bin/prngrfx
    rm -f /helios/winsrvr.exe /helios/include/graph.h /helios/lib/graph.lib
    rm -f /helios/local/bin/startws /helios/local/bin/mslights
    rm -f $mswin_include/graph.h
    rm -rf $mswin_examples

    echo Helios MS Windows 3.1 I/O Server Deleted
  endif

exit $exitcode
