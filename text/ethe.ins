# Ethernet 1.04 500
# $Id: ethe.ins,v 1.7 1992/04/12 18:24:22 craig Exp $
set exitcode=0

set version_no	= 1.04

set bin		= /helios/bin
set lib		= /helios/lib
set etc		= /helios/etc
set inc		= /helios/include
set sys		= /helios/system

set bin_files	= "ftp ping rcp rlogin rsh setterm telnet"
set lib_files	= "inetd tcpip"
set lib_files   = "$lib_files tpseudo.d tserial.d ttyserv"
set lib_files   = "$lib_files ftpd rexecd rlogind rshd telnetd"
set lib_files   = "$lib_files ether.d hpt02.dev imsb431.b2h in-ether.d"
set lib_files   = "$lib_files pc-ether.d sq-ether.d"
set etc_files	= "devinfo.net hosts hosts.equiv inetd.conf networks protocols"
set etc_files	= "$etc_files services socket.conf termcap"
set inc_dirs	= "$inc/net $inc/netinet $inc/arpa"

set system_dirs	= "$bin $lib $inc $sys"

set local_dir	= /helios/local
set tcpip_dir	= $local_dir/tcpip
set driver_dir	= $tcpip_dir/pc-ether
set socket_dir	= $tcpip_dir/examples
set example_dirs= ($driver_dir $socket_dir)

set read_me	= $sys/ether104.txt

if ($argv[1] != 0) then

echo Installing Helios Ethernet I V$version_no
#installation script
  #argv[1] = 0 for uninstalling software, otherwise the software is installed
  #argv[2] = dirname of the installation disk or tape drive
  #argv[3] = installation medium "tape" or "disk"
  switch ($argv[3])
    case disk:

# include directories
      foreach dir ($inc $inc_dirs)
        test -d $dir
        if ($status == 1) then
          echo Creating directory: $dir
          mkdir $dir
        endif
      end

# system files
      foreach dir ($system_dirs)
        echo Installing files to: $dir
        cp -r $argv[2]$dir/* $dir 
        if($status != 0) then
          set exitcode=1
        endif
      end

# database files
      echo
      echo "Installing Network Database files (optional)"
      cp -i $argv[2]$etc/* $etc
      if($status != 0) then
        set exitcode=1
      endif

# optional examples ...
      echo
      echo Example programs ...
      foreach exam_dir ($example_dirs)
        if ($exam_dir == $driver_dir) then
          echo "Install pc-ethernet driver example: $exam_dir (y/n) ?"
        else
          echo "Install tcpip socket example: $exam_dir (y/n) ?"
        endif
        set tempflag=$<
        if ($tempflag == y || $tempflag == Y) then
          foreach target_dir ($local_dir $tcpip_dir $exam_dir)
            test -d $target_dir
            if ($status == 1) then
              mkdir $target_dir
            endif
          end
          echo Installing directory: $exam_dir
          cp -r $argv[2]$exam_dir/* $exam_dir
        else
          echo Not installed
        endif
      end

      rehash

      echo 
      echo "The following information pertains to Version $version_no. A copy of these notes can"
      echo "be found in the file $read_me"
      echo "Press <Enter> to continue ..."
      set tempflag=$<
      more $read_me
      echo 
      
      echo Installation complete
      echo 
      echo Please refer to the Ethernet I Guide for further installation details

      breaksw
    case tape:
       echo No tape support for loadpac yet
       breaksw
    endsw
else
#remove instructions

    echo Removing files from: $bin ...
    cd $bin
    rm -f $bin_files
    echo Removing files from: $lib ...
    cd $lib
    rm -f $lib_files
    echo Removing $inc sub_directories ...
    rm -rf $inc_dirs

    echo "Remove Network Database files (y/n) ?"
    set tempflag=$<
    if ($tempflag == y || $tempflag == Y) then
        cd $etc
        rm -f $etc_files
    else
      echo Not removed
    endif

    echo "Removing local directories (optional)"
    foreach dir ($tcpip_dir $driver_dir $socket_dir)
      test -d $dir
      if ($status == 0) then
        echo "Remove directory: $dir (y/n) ?"
        set tempflag=$<
        if ($tempflag == y || $tempflag == Y) then
          echo Removing: $dir
          rm -rf $dir
        else
          echo Not removed
        endif
      endif
    end

    rm -f $read_me

endif
exit $exitcode
