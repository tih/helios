/* make for micrognuemacs on primos
/* by Robert A. Larson
&args como:-como; ph:-ph; v64v: -64v;
&if ^ [null %como%] &then como make.como
&if ^ [null %ph%] &then &do
  chap idle
&end
&do f &list *>sys>prime>sysdef.h *>sys>default>ttydef.h *>sys>default>chrdef.h
  &if ^ [exists [entryname %f%]] &then copy %f% -rpt
  &else &if [attrib [entryname %f%] -dtm] < [attrib %f% -dtm] ~
    &then copy %f% -nq -rpt
&end
&set_var w := [attrib def.h -dtm]
&if %w% < [attrib sysdef.h -dtm] &then &set_var w := [attrib sysdef.h -dtm]
&if %w% < [attrib ttydef.h -dtm] &then &set_var w := [attrib ttydef.h -dtm]
&if %w% < [attrib chrdef.h -dtm] &then &set_var w := [attrib chrdef.h -dtm]
&if [null %v64v%] &then &do
  &if %w% < [attrib *>sys>prime>mg.options.c -dtm] ~
    &then &set_var w := [attrib *>sys>prime>mg.options.c -dtm]
  &set_var binext := .bin
&end
&else &do
  &if %w% < [attrib *>sys>prime>mg.64v.options.c -dtm] ~
    &then &set_var w := [attrib *>sys>prime>mg.64v.options.c -dtm]
  &set_var binext := .64v.bin
  &set_var m := ^ [exists stackptr$.64v.bin]
  &if ^ %m% &then &do
    &set_var bw := [attrib stackptr$.64v.bin -dtm]
    &set_var m := [attrib *>sys>prime>stackptr$.pma -dtm] > %bw% | %w% > %bw%
  &end
  &if %m% &then pma *>sys>prime>stackptr$.pma -list no -bin stackptr$.64v.bin
&end
&s dir := *
&do f &list basic.c buffer.c dir.c dired.c display.c echo.c extend.c file.c ~
    help.c kbd.c keymap.c line.c macro.c main.c match.c modes.c paragraph.c ~
    random.c region.c search.c version.c window.c word.c
  &call compile
&end
&s dir := *>sys>prime
&do f &list cinfo.c fileio.c spawn.c ttyio.c
  &call compile
&end
&s dir := *>sys>default
&do f &list tty.c
  &call compile
&end
&s dir := *>termlib
&do f &list fgetlr.c tgetent.c tgetflag.c tgetnum.c tgetstr.c tgoto.c tputs.c
  &call compile
&end
date
time
&set_var u := 0
&data bind
  version mg 2a
  no_wildcard
  no_iteration
  li ccmain
  &do f &items [wild @%binext% -single u]
    lo %f%
  &end
  li c_lib
  li
  dynt -all
  rdc
  &if [null %v64v%] &then &do
    map mg.map
    file mg.run
  &end
  &else &do
    map mg.64v.map
    file mg.64v.run
  &end
&end
date
time
&if ^ [null %como%] &then como -end
&return

&routine compile
      &set_var b := [before %f% '.']%binext%
      &set_var f := %dir%>%f%
      &set_var m := ^ [exists %b%]
      &if ^ %m% &then &do             /* avoid attrib on non-existant file
        &set_var bw := [attrib %b% -dtm]
        &set_var m := [attrib %f% -dtm] > %bw% | %w% > %bw%
      &end
      &if %m% &then &do
        type %f%
        date
        &if [null %v64v%] &then &do
          ci %f% -optionsfile *>sys>prime>mg.options.c -bin %b%
        &end
        &else &do
          cc %f% -optionsfile *>sys>prime>mg.64v.options.c -bin %b%
        &end
      &end
&return


