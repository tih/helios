         NOLIST
         INCLUDE     "tripos.i"
         LIST

         XDEF        _DOSBase

PARM0    MACRO
         XDEF        \1
\1       MOVE.L      A1,-(SP)
         MOVE.L      _DOSBase,A0
         JSR         _LVO\1(A0)
         MOVE.L      (SP)+,A1
         RTS
         ENDM

PARM1    MACRO
         XDEF        \1
\1       MOVE.L      A1,-(SP)
         MOVE.L      D0,D1
         MOVE.L      _DOSBase,A0
         JSR         _LVO\1(A0)
         MOVE.L      (SP)+,A1
         RTS
         ENDM

PARM2    MACRO
         XDEF        \1
\1       MOVE.L      A1,-(SP)
         MOVE.L      D1,D2
         MOVE.L      D0,D1
         MOVE.L      _DOSBase,A0
         JSR         _LVO\1(A0)
         MOVE.L      (SP)+,A1
         RTS
         ENDM

PARM3    MACRO
         XDEF        \1
\1       MOVE.L      A1,-(SP)
         MOVE.L      D2,D3
         MOVE.L      D1,D2
         MOVE.L      D0,D1
         MOVE.L      _DOSBase,A0
         JSR         _LVO\1(A0)
         MOVE.L      (SP)+,A1
         RTS
         ENDM

PARM4    MACRO
         XDEF        \1
\1       MOVEM.L     A1/D4,-(SP)
         MOVE.L      D3,D4
         MOVE.L      D2,D3
         MOVE.L      D1,D2
         MOVE.L      D0,D1
         MOVE.L      _DOSBase,A0
         JSR         _LVO\1(A0)
         MOVEM.L     (SP)+,D4/A1
         RTS
         ENDM

         PARM1       Exit
         PARM1       Delay
         PARM0       Input
         PARM0       Output
         PARM1       UnLoadSeg
         PARM2       WaitForChar
         PARM1       Close
         PARM1       UnLock
         PARM1       DupLock
         PARM3       Seek
         PARM3       Read
         PARM3       Write
         PARM1       ParentDir
         PARM3       Execute
         PARM1       IsInteractive
         PARM1       DateStamp
         PARM2       SetProtection
         PARM2       SetComment
         PARM1       DeviceProc
         PARM1       LoadSeg
         PARM4       CreateProc
         PARM0       IoErr
         PARM1       CurrentDir
         PARM1       CreateDir
         PARM2       Info
         PARM2       ExNext
         PARM2       Examine
         PARM2       Lock
         PARM2       Rename
         PARM1       DeleteFile
         PARM2       Open
         PARM2       PktWait
         PARM4       VDU

         DATA
_DOSBase: DC.L       0
         END
